<Unit> -> <Decl> ; | <Decl> ; <Unit>

<Decl> -> enum <EnumDecl> | struct <TypeDecl> | var <VarDecl> | const <ConstDecl> | func <FuncDecl>

<EnumDecl> -> name { <EnumItems> }
<EnumItems> -> <EnumItem> | <EnumItem> , <EnumItems>
<EnumItem> -> name | name = <Expr>

<TypeDecl> -> name { <TypeFields> }
<TypeFields> -> <NameList> : <Type> ; | <NameList> : <Type> ; <TypeFields>

<NameList> -> name | name , <NameList>
<VarDecl> -> name = <Expr> | name : <Type> <VarDeclWithInit>
<VarDeclWithInit> -> = <Expr> | e

<ConstDecl> -> name = <Expr> | name : <Type> = <Expr>

<FuncDecl> -> name ( <FuncParamList> ) { <StatementBlock> } | name ( <FuncParamList> ) : <Type> { <StatementBlock> }

<FuncParamList> -> <FuncParam> | <FuncParam> , <FuncParamList> | e
<FuncParam> -> name : <Type>

<StatementBlock> -> <Statement> <A>
<A> -> <StatementBlock> | e

<Statement> -> return <Expr> ; | return ;
<Statement> -> break ;
<Statement> -> continue ;
<Statement> -> { <StatementBlock> }
<Statement> -> if ( <Expr> ) { <StatementBlock> } <ElseIf>
<ElseIf> -> else { <StatementBlock> } | else if ( <Expr> ) { <StatementBlock> } <ElseIf> | e
<Statement> -> while ( <Expr> ) { <StatementBlock> }
<Statement> -> do { <StatementBlock> } while ( <Expr> )
<Statement> -> for ( <StatementList> ; <Expr> ; <StatementList> ) { <StatementBlock> } | for ( <StatementList> ; ; <StatementList> ) { <StatementBlock> }
<Statement> -> switch ( <Expr> ) <SwitchBlock>
<Statement> -> <AssignExpr> ;

<AssignExpr> -> INC <Expr>  | DEC <Expr> | <Expr> <IncOrDecOrAssign>
<IncOrDecOrAssign> -> INC | DEC | <AssignOp>
<AssignOp> -> = <Expr> | += <Expr> | -= <Expr> | %= <Expr> | /= <Expr> | : <Type> = <Expr> | e

<StatementList> -> <AssignExpr> <StatementListNext> | e
<StatementListNext> -> , <StatementList> | e

<Type> -> name | BoolKeyword | IntKeyword | StrKeyword | FloatKeyword | CharKeyword

<SwitchBlock> -> { <SwitchCase> } | { <SwitchCase> <SwitchBlock> }
<SwitchCase> ->  case <Expr> : <Statement> <SwitchCase> | default : <Statement>

<Expr> -> <OrExpr>
<OrExpr> -> <AndExpr> OR <OrExpr> | <AndExpr>
<AndExpr> ->  <CmpExpr> AND <AndExpr> | <CmpExpr>
<CmpExpr> -> <AddExpr> <CmpOp> <CmpExpr> | <AddExpr>
<CmpOp> -> Eq | NotEq | GT | LT | LTEq | GTEq
<AddExpr> -> <MulExpr> <AddOp> <AddExpr> | <MulExpr>
<AddOp> -> Plus | Minus
<MulExpr> -> <UnaryExpr> <MulOp> <MulExpr> | <UnaryExpr>
<MulOp> -> Mul | Div | Mod
<UnaryExpr> -> <UnaryOp> <UnaryExpr> | <BaseExpr>
<UnaryOp> -> Plus | Minus | Not
<BaseExpr> -> <OperandExpr>
<OperandExpr> -> Int | Float | Str | <TypeFieldCall> | true | false | Char | ( <Expr> )
<TypeFieldCall> -> name . <TypeFieldCall> | name ( <ExprList> ) |  name

<ExprList> -> <Expr> <ExprListNext> | e
<ExprListNext> -> , <ExprList> | e